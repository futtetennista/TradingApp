apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.futtetennista.trading"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api_endpoint"

    productFlavors {
        beta {
            buildConfigField("String", "REST_ENDPOINT", "\"https://api.beta.server.com\"")
            buildConfigField("String", "WEBSOCKET_ENDPOINT", "\"https://ws.beta.server.com\"")
            dimension "api_endpoint"
        }

        sandbox {
            buildConfigField("String", "REST_ENDPOINT", "\"http://10.0.2.2:8080\"")
            buildConfigField("String", "WEBSOCKET_ENDPOINT", "\"http://10.0.2.2:8080\"")
            dimension "api_endpoint"
        }
    }

    variantFilter { variant ->
        def fnames = variant.flavors*.name
        def btnames = variant.buildType*.name
        ignore = fnames.contains("sandbox") && btnames.contains("release")
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation('io.reactivex.rxjava2:rxkotlin:2.1.0', {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava2'
    })
    implementation('io.reactivex.rxjava2:rxandroid:2.0.1', {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava2'
    })
    implementation('com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0', {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava2'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
    })
    implementation 'com.github.pwittchen:reactivenetwork-rx2:0.12.1'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
}
